//
//
//	Author		Shakto
//	Version		2024/09/01
//	Web		https://ronovelty.com/
//
//

function	script	F_autoattacktoggle	{
//10000 = 10 seconds
#aa_duration = 86400;
goto(main_menu);
end;

main_menu:
	.@menu$ = "[AutoAttack Configuration]";
	mes .@menu$;
	
	if(!rentalcountitem(14316)){
		mes "You need to have an AutoBuff Device to use this feature";
		close;
	}
	
	.@main_menu = select(aa_getstate()?"[Stop]":"[Start]","[Configuration]","[Cancel]");
	switch(.@main_menu){
		case 1:
			if(aa_getstate())
				sc_end SC_AUTOATTACK;
			else
				autoattack_fromitem(14316);
			break;
		case 2:
			goto(config_menu);
			break;
	}
	end;

config_menu:
	clear;
	.@menu$ = "[AutoAttack Configuration]";
	mes .@menu$;

	// To disable an option, comment the line
	menu	"Return",main_menu,
			" ~ Regeneration",regeneration_menu,
			" ~ Attack Skills",attackskills_menu,
			" ~ Buff Skills",buffskills_menu,
			" ~ Buff Items",buffitems_menu,
			" ~ Monsters Selection",monsters_menu,
			" ~ Items Pickup Selection",pickupitems_menu,
			" ~ Teleport",teleport_menu,
			" ~ Others configurations",others_menu,
			" ~ Reset all",reset_all;
	end;

pickupitems_menu:
	clear;
	.@menu$ = "[Items Pickup Selection]";

	mes .@menu$;
	mes "--- Current configuration ---";
	mes "Select the items to pick up";
	mes aa_getautoattackstring(13,0);

	.@pickupitems_menu = select("Pick up configuration","Add an item","Remove an item","Clear the list","[Cancel]");

	switch(.@pickupitems_menu){
		case 1:
			.@pickup_config_choice = 0;
			// every item = 0
			// on the list = 1
			// no item = 2
			switch(aa_getautoattackint(9,0)){
				case 0:
					.@pickupitems_config_menu  = select("Pick up only items on the list","Don't pick up any item","[Cancel]");
					switch(.@pickupitems_config_menu){
						case 1:
							.@pickup_config_choice = 1;
							break;
						case 2 :
							.@pickup_config_choice = 2;
							break;
						case 3 :
							goto(pickupitems_menu);
							break;
					}
						
					break;
				case 1:
					.@pickupitems_config_menu  = select("Pick up every items","Don't pick up any item","[Cancel]");
					switch(.@pickupitems_config_menu){
						case 1:
							.@pickup_config_choice = 0;
							break;
						case 2 :
							.@pickup_config_choice = 2;
							break;
						case 3 :
							goto(pickupitems_menu);
							break;
					}
					break;
				case 2:
					.@pickupitems_config_menu  = select("Pick up every items","Pick up only items on the list","[Cancel]");
					switch(.@pickupitems_config_menu){
						case 1:
							.@pickup_config_choice = 0;
							break;
						case 2 :
							.@pickup_config_choice = 1;
							break;
						case 3 :
							goto(pickupitems_menu);
							break;
					}
					break;				
			}
			aa_setautoattack("20;" + .@pickup_config_choice);
			break;
		case 2:
			clear;
			mes .@menu$;
			mes "Enter the id of the item to add on the list (20 max)";
			do {
				input .@itempickup_id;
			} while(.@itempickup_id < 1);

			aa_setautoattack("21;1;" + .@itempickup_id);

			break;
		case 3:
			if(aa_getautoattackint(17,0) > 0){
				mes "Enter the id of the item to delete from the list";

				do {
					input .@itempickup_id;
				} while(.@itempickup_id < 1);
				aa_setautoattack("21;0;" + .@itempickup_id);
			} else {
				clear;
				mes .@menu$;
				mes "Sorry, you don't have any item of this list";
				next;
			}

			break;
		case 4:
			aa_setautoattack("21;-1;0");
			break;

		case 5:
			goto(config_menu);
			break;
	}

	goto(pickupitems_menu);
	end;

others_menu:
	clear;
	.@menu$ = "[Others configurations]";

	mes .@menu$;
	//mes "--- Current configuration ---";
	//mes aa_getautoattackstring(9,0);
	//mes aa_getautoattackstring(8,0);
	//mes aa_getautoattackstring(10,0);
	//mes aa_getautoattackstring(11,0);

	.@others_menu = select(
	aa_getautoattackint(7,0)?"^008000(ON)^000000 Token of Siegfried":"^FF0000(OFF)^000000 Token of Siegfried"
	,aa_getautoattackint(6,0)?"^008000(ON)^000000 Return to save point":"^FF0000(OFF)^000000 Return to save point"
	,aa_getautoattackint(8,0)?"^008000(ON)^000000 Auto accept party request":"^FF0000(OFF)^000000 Auto accept party request"
	,aa_getautoattackint(10,0)?"^008000(ON)^000000 Priorize loot over fight":"^FF0000(OFF)^000000 Priorize loot over fight"
	,aa_getautoattackint(11,0)==1?"^FF0000(OFF)^000000 Melee attack":"^008000(ON)^000000 Melee attack"
	,aa_getautoattackint(11,0)==2?"^008000(ON)^000000 Melee attack only when SP below 100sp":"^FF0000(OFF)^000000 Melee attack only when SP below 100sp"
	,aa_getautoattackint(16,0)?"^008000(ON)^000000 Ignore aggressive monster if not in the list":"^FF0000(OFF)^000000 Ignore aggressive monster if not in the list"
	,"Configure action when autoattack stop"
	,"[Cancel]"
	);
	switch(.@others_menu){
		case 1:
			if(aa_getautoattackint(7,0))
				aa_setautoattack("11;0");
			else
				aa_setautoattack("11;1");
			break;
		case 2:
			if(aa_getautoattackint(6,0))
				aa_setautoattack("9;0");
			else
				aa_setautoattack("9;1");
			break;
		case 3:
			if(aa_getautoattackint(8,0))
				aa_setautoattack("12;0");
			else
				aa_setautoattack("12;1");
			break;
		case 4:
			if(aa_getautoattackint(10,0))
				aa_setautoattack("13;0");
			else
				aa_setautoattack("13;1");
			break;
		case 5:
			if(aa_getautoattackint(11,0) == 1)
				aa_setautoattack("7;0");
			else
				aa_setautoattack("7;1");
			break;
		case 6:
			if(aa_getautoattackint(11,0) == 2)
				aa_setautoattack("7;0");
			else
				aa_setautoattack("7;2");
			break;
		case 7:
			if(aa_getautoattackint(16,0))
				aa_setautoattack("18;0");
			else
				aa_setautoattack("18;1");
			break;
		case 8:
			goto(action_on_end_menu);
			break;
		case 9:
			goto(config_menu);
			break;
	}
	goto(others_menu);

	end;

action_on_end_menu:
	clear;
	.@menu$ = "[Action when autoattack stop]";

	mes .@menu$;
	mes "--- Current configuration ---";
	mes aa_getautoattackstring(4,0); // status hp sp

	.@action_on_end_menu = select(
	aa_getautoattackint(18,0) == 0?"^008000[x]^000000 Do nothing":"[ ] Do nothing"
	,aa_getautoattackint(18,0) == 1?"^008000[x]^000000 Teleport to save point":"[ ] Teleport to save point"
	,aa_getautoattackint(18,0) == 2?"^008000[x]^000000 Logout":"[ ] Logout"
	,"[Cancel]"
	);

	switch(.@action_on_end_menu){
		case 1:
			if(aa_getautoattackint(18,0) != 0)
				aa_setautoattack("22;0");
			break;
		case 2:
			if(aa_getautoattackint(18,0) != 1)
				aa_setautoattack("22;1");
			break;
		case 3:
			if(aa_getautoattackint(18,0) != 2)
				aa_setautoattack("22;2");
			break;
		case 4:
			clear;
			goto(others_menu);
			break;
	}

	clear;
	goto(others_menu);
	end;

reset_all:
	clear;
	.@menu$ = "[Reset settings]";
	mes .@menu$;
	
	.@reset_menu = select("Confirm the reset of all settings","[Cancel]");

	switch(.@reset_menu){
		case 1:
			aa_setautoattack("10;0");
			break;
	}

	goto(config_menu);
	end;

monsters_menu:
	clear;
	.@menu$ = "[Monster Selection]";

	mes .@menu$;
	mes "--- Current configuration ---";
	mes "Select here the monsters that you don't want to attack";
	//deletearray .@mobs_menu_ids;
	//deletearray .@mobs_menu_state;
	//deletearray .@mobs_menu_list$;
	mes aa_getautoattackstring(12,0);
	
	if(getarraysize(.@mobs_menu_ids) == 0){
		clear;
		mes .@menu$;
		mes "Sorry, there is no monsters on the map";
		next;
		goto(main_menu);
	}
	
	.@mobs_menu_list$ = "[Cancel]:" + .@mobs_menu_list$;
	.@i = select( .@mobs_menu_list$ );

	if(.@i == 1)
		goto(config_menu);
	else
		.@i--;

	if(.@mobs_menu_state[.@mobs_menu_ids[.@i]])
		aa_setautoattack("19;"+ .@mobs_menu_ids[.@i] + ";0");
	else
		aa_setautoattack("19;"+ .@mobs_menu_ids[.@i] + ";1");

	goto(monsters_menu);
	end;

teleport_menu:
	clear;
	.@menu$ = "[Teleport]";

	mes .@menu$;
	mes "--- Current configuration ---";
	mes aa_getautoattackstring(6,0);

	.@teleport_menu = select(
	aa_getautoattackint(12,0)?"^FF0000(OFF)^000000 Enable fly wing to teleport":"^008000(ON)^000000 Enable fly wing to teleport"
	,aa_getautoattackint(13,0)?"^FF0000(OFF)^000000 Enable teleport skill to teleport":"^008000(ON)^000000 Enable teleport skill to teleport"
	,"Set the minimum HP value for emergency teleport"
	,"Adjust the teleport delay if no monster is encountered within that time"
	,"Adjust the max number of monster that can target you before being teleported"
	,"[Cancel]"
	);
	
	switch(.@teleport_menu){
		case 1:
			if(aa_getautoattackint(12,0))
				aa_setautoattack("4;0");
			else
				aa_setautoattack("4;1");
			break;
		case 2:
			if(aa_getautoattackint(13,0))
				aa_setautoattack("6;0");
			else
				aa_setautoattack("6;1");
			break;
		case 3:
			do {
				clear;
				mes .@menu$;
				mes "Choose the minimum hp % for emergency teleport)";
				mes "Min value is 5, Max value is 90 (0 to disable it)";
				input .@teleport_minhpvalue;
			} while(.@teleport_minhpvalue != 0 && (.@teleport_minhpvalue < 5 || .@teleport_minhpvalue > 90));
			aa_setautoattack("16;"+.@teleport_minhpvalue);
			break;
		case 4:
			do {
				clear;
				mes .@menu$;
				mes "Adjust the teleport delay (in seconds) if no monster is encountered within that time";
				mes "Min value is 2, Max value is 600 (0 to disable it)";
				input .@teleport_delayvalue;
			} while(.@teleport_delayvalue != 0 && (.@teleport_delayvalue < 2 || .@teleport_delayvalue > 600));
			.@teleport_delayvalue = .@teleport_delayvalue * 1000;
			aa_setautoattack("17;"+.@teleport_delayvalue);
			break;
		case 5:
			do {
				clear;
				mes .@menu$;
				mes "Adjust the max number of monster that can target you before being teleported";
				mes "Max value is 30 (0 to disable it)";
				input .@monster_surround;
			} while(.@monster_surround != 0 && (.@monster_surround < 0 || .@monster_surround > 30));
			aa_setautoattack("23;"+.@monster_surround);
			break;
		case 6:
			goto(config_menu);
			break;
	}

	goto(teleport_menu);
	end;

regeneration_menu:
	mes "Clear menu";
	clear;
	.@menu$ = "[Regeneration]";
	
	mes .@menu$;
	menu	"Return",config_menu,
			" ~ Auto-Heal",heal_menu,
			" ~ HP / SP Potions",potion_menu,
			" ~ Sit to Rest",sit_regen_menu;
	end;

heal_menu:
	clear;
	.@menu$ = "[Auto-Heal]";

	mes .@menu$;
	mes "--- Current configuration ---";
	mes "You can add / modify or remove the following skills";
	setarray .@lheal_skill[0],28,2051,2043;

	.@healskill_menu1$ = "";
	.@j = 0;
	
	// Check if the player have the skills
	for (.@i = 0; .@i < getarraysize(.@lheal_skill); .@i++){
		if(getskilllv(.@lheal_skill[.@i]))
			.@lheal_skill2[.@j++] = .@lheal_skill[.@i];
	}

	if(getarraysize(.@lheal_skill2) == 0){
		clear;
		mes .@menu$;
		mes "Sorry, you don't have any kind of heal skills allowed";
		next;
		goto(regeneration_menu);
	}

	for (.@i = 0; .@i < getarraysize(.@lheal_skill2); .@i++)
		.@healskill_menu1$ = .@healskill_menu1$ + (.@i > 0 ? ":" : "") + aa_getautoattackstring(0,.@lheal_skill2[.@i],1);

	.@i = select(.@healskill_menu1$) -1;

	if(.@skill_menu_state[.@lheal_skill2[.@i]]){
		.@heal_menu = select("Modify","Disable","[Cancel]");
		switch(.@heal_menu){
			case 1 :
				do{
					clear;
					mes .@menu$;
					mes "Choose the skill lv to use";
					mes "Max lv : " + getskilllv(.@lheal_skill2[.@i]);
					mes "";
					mes "Current configuration : ";
					mes aa_getautoattackstring(0,.@lheal_skill2[.@i],0);
					input .@heal_skilllv;
				} while(.@heal_skilllv < 1 || .@heal_skilllv > getskilllv(.@lheal_skill2[.@i]));

				do {
					clear;
					mes .@menu$;
					mes "Choose the value of HP % to use the skill (ex : 10 to use skill if hp are below 10%)";
					mes "Min value is 5, Max value is 90";
					input .@heal_minhpvalue;
				} while(.@heal_minhpvalue < 5 || .@heal_minhpvalue > 90);


				aa_setautoattack("0;1;"+ .@lheal_skill2[.@i] + ";" + .@heal_skilllv + ";" + .@heal_minhpvalue);
				break;

				goto(heal_menu);
				break;
			case 2:
				aa_setautoattack("0;0;"+ .@lheal_skill[.@i] + ";0;0");
				goto(heal_menu);
				break;
			case 3:
				goto(heal_menu);
				break;
		}
	} else {
		.@heal_menu = select("Enable","[Cancel]");
		switch(.@heal_menu){
			case 1 :
				do{
					clear;
					mes .@menu$;
					mes "Choose the skill lv to use";
					mes "Max lv : " + getskilllv(.@lheal_skill2[.@i]);
					mes "";
					mes "Current configuration : ";
					mes aa_getautoattackstring(0,.@lheal_skill2[.@i],0);
					input .@heal_skilllv;
				} while(.@heal_skilllv < 1 || .@heal_skilllv > getskilllv(.@lheal_skill2[.@i]));

				do {
					clear;
					mes .@menu$;
					mes "Choose the value of HP % to use the skill (ex : 10 to use skill if hp are below 10%)";
					mes "Min value is 5, Max value is 90";
					input .@heal_minhpvalue;
				} while(.@heal_minhpvalue < 5 || .@heal_minhpvalue > 90);

				aa_setautoattack("0;1;"+ .@lheal_skill2[.@i] + ";" + .@heal_skilllv + ";" + .@heal_minhpvalue);
				break;

				goto(heal_menu);
				break;
			case 2:
				goto(heal_menu);
				break;
		}
	}

	goto(heal_menu);
	end;

potion_menu:
	clear;
	.@menu$ = "[HP / SP Potions]";
	mes .@menu$;
	mes "--- Current configuration ---";
	mes "You can add / modify or remove the following potion";
	
	//deletearray .@potion_menu_ids;
	//deletearray .@potion_menu_state;
	//deletearray .@potion_menu_list$;
	setarray .@lpotion_disable_list[0],14875,14876;
	mes aa_getautoattackstring(1,0);
	
	if(getarraysize(.@potion_menu_ids) == 0){
		clear;
		mes .@menu$;
		mes "Sorry, you don't have any kind of heal items";
		next;
		goto(regeneration_menu);
	}
	
	.@potion_menu_list$ = "[Cancel]:" + .@potion_menu_list$;
	.@i = select( .@potion_menu_list$ );

	if(.@i == 1)
		goto(regeneration_menu);
	else
		.@i--;
	
	if(.@potion_menu_state[.@potion_menu_ids[.@i]]){
		// Check if the selected the potion
		for (.@j = 0; .@j < getarraysize(.@lpotion_disable_list); .@j++){
			if(.@potion_menu_ids[.@i] == .@lpotion_disable_list[.@j]){
				mes "You can't select that item. it has been disabled by GM on autoattack";
				next;
				goto(potion_menu);
			}
		}
	
		.@potion_menu2 = select("Modify","Disable","[Cancel]");
		switch(.@potion_menu2){
			case 1:
				do {
					clear;
					mes .@menu$;
					mes "Choose the value of HP % to use the consumable (ex : 10 to use it if hp are below 10%)";
					mes "Min value is 5, Max value is 90 (0 to disable it)";
					input .@potion_minhpvalue;
				} while(.@potion_minhpvalue != 0 && (.@potion_minhpvalue < 5 || .@potion_minhpvalue > 90));

				do {
					clear;
					mes .@menu$;
					mes "Choose the value of SP % to use the consumable (ex : 10 to use it if sp are below 10%)";
					mes "Min value is 5, Max value is 90 (0 to disable it)";
					input .@potion_minspvalue;
				} while(.@potion_minspvalue != 0 && (.@potion_minspvalue < 5 || .@potion_minspvalue > 90));

				aa_setautoattack("1;1;"+ .@potion_menu_ids[.@i] + ";" + .@potion_minhpvalue + ";" + .@potion_minspvalue);
				goto(potion_menu);
				break;
			case 2:
				aa_setautoattack("1;0;"+ .@potion_menu_ids[.@i] + ";0;0");
				goto(potion_menu);
				break;
			case 3:
				goto(potion_menu);
				break;
		}
	}
	else{
		.@potion_menu2 = select("Add","[Cancel]");
		switch(.@potion_menu2){
			case 1:
				do {
					clear;
					mes .@menu$;
					mes "Choose the value of HP % to use the consumable (ex : 10 to use it if hp are below 10%)";
					mes "Min value is 5, Max value is 90 (0 to disable it)";
					input .@potion_minhpvalue;
				} while(.@potion_minhpvalue != 0 && (.@potion_minhpvalue < 5 || .@potion_minhpvalue > 90));

				do {
					clear;
					mes .@menu$;
					mes "Choose the value of SP % to use the consumable (ex : 10 to use it if sp are below 10%)";
					mes "Min value is 5, Max value is 90 (0 to disable it)";
					input .@potion_minspvalue;
				} while(.@potion_minspvalue != 0 && (.@potion_minspvalue < 5 || .@potion_minspvalue > 90));

				aa_setautoattack("1;1;"+ .@potion_menu_ids[.@i] + ";" + .@potion_minhpvalue + ";" + .@potion_minspvalue);
				goto(potion_menu);
				break;
			case 2:
				goto(potion_menu);
				break;
		}
	}
	
	goto(regeneration_menu);
	end;

attackskills_menu:
	clear;
	.@menu$ = "[Attack Skills]";

	mes .@menu$;
	mes "--- Current configuration ---";
	mes "You can add / modify or remove the following skills";
	//deletearray .@skill_menu_ids;
	//deletearray .@skill_menu_state;
	//deletearray .@skill_menu_list$;
	mes aa_getautoattackstring(2,0);
	
	if(getarraysize(.@skill_menu_ids) == 0){
		clear;
		mes .@menu$;
		mes "Sorry, you don't have any kind of buff skills";
		next;
		goto(main_menu);
	}
	
	.@skill_menu_list$ = "[Cancel]:" + .@skill_menu_list$;
	.@i = select( .@skill_menu_list$ );

	if(.@i == 1)
		goto(config_menu);
	else
		.@i--;

	if(.@skill_menu_state[.@skill_menu_ids[.@i]]){
		.@skill_menu2 = select("Modify","Disable","[Cancel]");
		switch(.@skill_menu2){
			case 1:
				do{
					clear;
					mes .@menu$;
					mes "Choose the skill lv to use";
					mes "Max lv : " + getskilllv(.@skill_menu_ids[.@i]);
					mes "";
					mes "Current configuration : ";
					mes aa_getautoattackstring(2,.@skill_menu_ids[.@i],1);
					input .@attack_skilllv;
				} while(.@attack_skilllv < 1 || .@attack_skilllv > getskilllv(.@skill_menu_ids[.@i]));

				clear;
				mes .@menu$;
				mes "Choose your buff configuration";

				aa_setautoattack("2;1;"+ .@skill_menu_ids[.@i] + ";" + .@attack_skilllv);

				goto(attackskills_menu);
				break;
			case 2:
				aa_setautoattack("2;0;"+ .@skill_menu_ids[.@i] + ";0");
				goto(attackskills_menu);
				break;
			case 3:
				goto(config_menu);
				break;
		}
	}
	else{
		.@skill_menu2 = select("Add","[Cancel]");
		switch(.@skill_menu2){
			case 1:
				do{
					clear;
					mes .@menu$;
					mes "Choose the skill lv to use";
					mes "Max lv : " + getskilllv(.@skill_menu_ids[.@i]);
					mes "";
					mes "Current configuration : ";
					mes aa_getautoattackstring(2,.@skill_menu_ids[.@i],1);
					input .@attack_skilllv;
				} while(.@attack_skilllv < 1 || .@attack_skilllv > getskilllv(.@skill_menu_ids[.@i]));

				clear;
				mes .@menu$;
				mes "Choose your buff configuration";
				
				aa_setautoattack("2;1;"+ .@skill_menu_ids[.@i] + ";" + .@attack_skilllv);
				
				goto(attackskills_menu);
				break;
			case 2:
				goto(config_menu);
				break;
		}
	}

	goto(config_menu);
	end;

buffskills_menu:
	clear;
	.@menu$ = "[Buff Skills]";

	mes .@menu$;
	mes "--- Current configuration ---";
	mes "You can add / modify or remove the following skills";
	//deletearray .@skill_menu_ids;
	//deletearray .@skill_menu_state;
	//deletearray .@skill_menu_list$;
	mes aa_getautoattackstring(3,0);
	
	if(getarraysize(.@skill_menu_ids) == 0){
		clear;
		mes .@menu$;
		mes "Sorry, you don't have any kind of buff skills";
		next;
		goto(main_menu);
	}

	.@skill_menu_list$ = "[Cancel]:" + .@skill_menu_list$;
	.@i = select( .@skill_menu_list$ );

	if(.@i == 1)
		goto(config_menu);
	else
		.@i--;

	if(.@skill_menu_state[.@skill_menu_ids[.@i]]){
		.@skill_menu2 = select("Modify","Disable","[Cancel]");
		switch(.@skill_menu2){
			case 1:
				do{
					clear;
					mes .@menu$;
					mes "Choose the skill lv to use";
					mes "Max lv : " + getskilllv(.@skill_menu_ids[.@i]);
					mes "";
					mes "Current configuration : ";
					mes aa_getautoattackstring(3,.@skill_menu_ids[.@i],1);
					input .@buff_skilllv;
				} while(.@buff_skilllv < 1 || .@buff_skilllv > getskilllv(.@skill_menu_ids[.@i]));

				clear;
				mes .@menu$;
				mes "Choose your buff configuration";

				aa_setautoattack("3;1;"+ .@skill_menu_ids[.@i] + ";" + .@buff_skilllv);

				goto(buffskills_menu);
				break;
			case 2:
				aa_setautoattack("3;0;"+ .@skill_menu_ids[.@i] + ";0");
				goto(buffskills_menu);
				break;
			case 3:
				goto(config_menu);
				break;
		}
	}
	else{
		.@skill_menu2 = select("Add","[Cancel]");
		switch(.@skill_menu2){
			case 1:
				do{
					clear;
					mes .@menu$;
					mes "Choose the skill lv to use";
					mes "Max lv : " + getskilllv(.@skill_menu_ids[.@i]);
					mes "";
					mes "Current configuration : ";
					mes aa_getautoattackstring(3,.@skill_menu_ids[.@i],1);
					input .@buff_skilllv;
				} while(.@buff_skilllv < 1 || .@buff_skilllv > getskilllv(.@skill_menu_ids[.@i]));

				clear;
				mes .@menu$;
				mes "Choose your buff configuration";
				
				aa_setautoattack("3;1;"+ .@skill_menu_ids[.@i] + ";" + .@buff_skilllv);
				
				goto(buffskills_menu);
				break;
			case 2:
				goto(config_menu);
				break;
		}
	}

	goto(config_menu);
	end;

buffitems_menu:
	clear;
	.@menu$ = "[Buff Items]";

	mes .@menu$;
	mes "--- Current configuration ---";
	setarray .@lbuff_item[0],645,656,657,12208,12210;
	setarray .@lbuff_sc[0],SC_ASPDPOTION0,SC_ASPDPOTION1,SC_ASPDPOTION2,SC_EXPBOOST,SC_ITEMBOOST;
	.@buffitems_menu1$ = "";
	
	for (.@i = 0; .@i < getarraysize(.@lbuff_item); .@i++)
		.@buffitems_menu1$ = .@buffitems_menu1$ + (.@i > 0 ? ":" : "") + aa_getautoattackstring(5,.@lbuff_item[.@i]);


	.@buffitems_menu1$ = "[Cancel]:" + .@buffitems_menu1$;
	.@i = select(.@buffitems_menu1$) -1;

	if(.@i == 0)
		goto(config_menu);
	else
		.@i--;
	
	.@buffitems_menu = select(.@buffitem_menu_state[.@lbuff_item[.@i]] ? "Disable" : "Enable","[Cancel]");
	if( .@buffitems_menu == 1 ){
		if(.@buffitem_menu_state[.@lbuff_item[.@i]])
			aa_setautoattack("5;0;"+ .@lbuff_item[.@i] + ";" + .@lbuff_sc[.@i]);
		else
			aa_setautoattack("5;1;"+ .@lbuff_item[.@i] + ";" + .@lbuff_sc[.@i]);
		goto(buffitems_menu);
	}
	else if( .@buffitems_menu == 2 ){
		aa_setautoattack("5;0;"+ .@lbuff_item[.@i] + ";0");
		goto(buffitems_menu);
	}
	
	goto(config_menu);
	end;

sit_regen_menu:
	clear;
	.@menu$ = "[Sit to Rest]";

	mes .@menu$;
	mes "--- Current configuration ---";
	mes aa_getautoattackstring(4,0); // status hp sp

	.@sit_regen_menu = select(
	aa_getautoattackint(14,0) == 1?"^008000(ON)^000000 Sit if HP < 75%":"^FF0000(OFF)^000000 Sit if HP < 75%"
	,aa_getautoattackint(14,0) == 2?"^008000(ON)^000000 Sit if HP < 50%":"^FF0000(OFF)^000000 Sit if HP < 50%"
	,aa_getautoattackint(14,0) == 3?"^008000(ON)^000000 Sit if HP < 25%":"^FF0000(OFF)^000000 Sit if HP < 25%"
	,aa_getautoattackint(15,0) == 1?"^008000(ON)^000000 Sit if SP < 75%":"^FF0000(OFF)^000000 Sit if SP < 75%"
	,aa_getautoattackint(15,0) == 2?"^008000(ON)^000000 Sit if SP < 50%":"^FF0000(OFF)^000000 Sit if SP < 50%"
	,aa_getautoattackint(15,0) == 3?"^008000(ON)^000000 Sit if SP < 25%":"^FF0000(OFF)^000000 Sit if SP < 25%"
	,"[Cancel]"
	);

	switch(.@sit_regen_menu){
		case 1:
			if(aa_getautoattackint(14,0) == 1)
				aa_setautoattack("14;0;0");
			else
				aa_setautoattack("14;75;100");
			break;
		case 2:
			if(aa_getautoattackint(14,0) == 2)
				aa_setautoattack("14;0;0");
			else
				aa_setautoattack("14;50;100");
			break;
		case 3:
			if(aa_getautoattackint(14,0) == 3)
				aa_setautoattack("14;0;0");
			else
				aa_setautoattack("14;25;100");
			break;
		case 4:
			if(aa_getautoattackint(15,0) == 1)
				aa_setautoattack("15;0;0");
			else
				aa_setautoattack("15;75;100");
			break;
		case 5:
			if(aa_getautoattackint(15,0) == 2)
				aa_setautoattack("15;0;0");
			else
				aa_setautoattack("15;50;100");
			break;
		case 6:
			if(aa_getautoattackint(15,0) == 3)
				aa_setautoattack("15;0;0");
			else
				aa_setautoattack("15;25;100");
			break;
		case 7:
			clear;
			goto(regeneration_menu);
			break;
	}

	clear;
	goto(sit_regen_menu);
	end;
}